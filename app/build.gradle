plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.example.androidbdd"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner getInstrumentation()
    }

    buildTypes {
        debug {
            buildConfigField 'String', 'TEST_TAGS', '"' + getTestTags() + '"'
            buildConfigField 'String', 'TEST_SCENARIO', '"' + getTestScenario() + '"'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

def getTestTags() {
    project.getProperties().get('tags') ?: ''
}

def getTestScenario() {
    project.getProperties().get('scenario') ?: ''
}

def getInstrumentation() {
    project.hasProperty('cucumber') ? 'com.example.androidbdd.cucumber.runner.CucumberTestRunner' : 'androidx.test.runner.AndroidJUnitRunner'
}

dependencies {

    //noinspection GradleDependency
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.multidex:multidex:2.0.1'
    testImplementation 'junit:junit:'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // BDD and Cucumber dependencies
    androidTestImplementation 'io.cucumber:cucumber-android:4.8.1'
    androidTestImplementation 'io.cucumber:cucumber-java:4.8.1'
    androidTestImplementation 'io.cucumber:cucumber-picocontainer:4.8.1'

    // allure dependencies
    androidTestImplementation "io.qameta.allure:allure-kotlin-model:2.2.6"
    androidTestImplementation "io.qameta.allure:allure-kotlin-commons:2.2.6"
    androidTestImplementation "io.qameta.allure:allure-kotlin-junit4:2.2.6"
    androidTestImplementation "io.qameta.allure:allure-kotlin-android:2.2.6"
}